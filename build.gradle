apply plugin: 'groovy'
apply plugin: 'idea'

group 'com.adisong.jenkins'
version '1.0-SNAPSHOT'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

configurations {
    ivy
}

repositories {
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    maven { url 'http://repo.spring.io/plugins-release/'}
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src','vars']
        }
    }

    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

dependencies {
    ivy "org.apache.ivy:ivy:2.4.0"

    compile 'org.codehaus.groovy:groovy-all:2.5.5'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.jenkins-ci.main:jenkins-core:2.150.3'
    compile 'com.cloudbees:groovy-cps:1.12'
    compile 'com.sonatype.nexus:nexus-platform-api:3.6.20190304-154512.2420969'


    // Jenkins plugins
    compile group: 'org.jenkins-ci.plugins', name: 'sonar', version: '2.8.1', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '2.19', ext: 'jar'
    compile group: 'org.sonatype.nexus.ci', name: 'nexus-jenkins-plugin', version: '3.5.20190313-114450.3bfee7f', ext: 'jar'

    testCompile group: 'com.lesfurets', name: 'jenkins-pipeline-unit', version: '1.1'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'

    // TRICKY: The lib folder contains all other plugins *JAR* files
    // if not found in Maven
    compile fileTree(dir: 'lib', include: ['*.jar'])
}

tasks.withType(GroovyCompile) {
    groovyClasspath += configurations.ivy
}